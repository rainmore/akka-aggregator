plugins {
    id "us.kirchmeier.capsule" version "1.0.1"
}

description = 'Aggregation'
group = 'net.rainmore'
version = '1.0'


apply plugin: 'scala'
apply plugin: 'application'
apply plugin: 'idea'

idea {
    project {
        jdkName = JavaVersion.VERSION_1_8
        languageLevel = JavaVersion.VERSION_1_8
        ipr {
            withXml {
                it.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping.@vcs = 'Git'
            }
        }
    }

    module {
        inheritOutputDirs = false
        outputDir = sourceSets.main.output.classesDir
        testOutputDir = sourceSets.test.output.classesDir
    }
}

mainClassName = [group, 'Main'].join('.')

repositories {
	mavenLocal() // for testing only
	mavenCentral() // for testing only
}

dependencies {
    compile group: 'org.scala-lang', name: 'scala-library', version: '2.11.7'
    compile group: 'com.typesafe',   name: 'config',        version: '1.3.0'

    compile group: 'com.typesafe.akka', name: 'akka-actor_2.11',   version: '2.4.0'
    compile group: 'com.typesafe.akka', name: 'akka-contrib_2.11', version: '2.4.0'
    compile group: 'com.typesafe.akka', name: 'akka-remote_2.11',  version: '2.4.0'
    compile group: 'com.typesafe.akka', name: 'akka-cluster_2.11', version: '2.4.0'
    compile group: 'com.typesafe.akka', name: 'akka-cluster-tools_2.11', version: '2.4.0'

    compile group: 'org.slf4j',                  name: 'slf4j-api',            version: '1.7.+'
    compile group: 'ch.qos.logback',             name: 'logback-classic',      version: '1.1.3'
    compile group: 'com.typesafe.akka',          name: 'akka-slf4j_2.11',      version: '2.4.0'
    compile group: 'com.typesafe.scala-logging', name: 'scala-logging_2.11',   version: '3.1.0'

    testCompile group: 'org.scalatest',     name: 'scalatest_2.11',               version: '2.2.4'
    testCompile group: 'com.typesafe.akka', name: 'akka-testkit_2.11',            version: '2.4.0'
    testCompile group: 'com.typesafe.akka', name: 'akka-multi-node-testkit_2.11', version: '2.4.0'
    testCompile group: 'org.jfairy',        name: 'jfairy',                       version: '0.3.0'
}

tasks.withType(ScalaCompile) {
    scalaCompileOptions.useAnt = false
}

ext.javaLintArgs = [
        '-Xlint:all',
        '-Xlint:-deprecation',
        '-Xlint:-path',
        '-Xlint:-serial'
]

ext.sharedManifest = manifest {
    attributes \
        'Implementation-Title':   project.name,
        'Implementation-Version': project.version,
        'Built-JDK':  System.getProperty('java.version'),
        'Built-By':   System.getProperty('user.name'),
        'Main-Class': project.mainClassName,
        'Built-Date': new Date()
}

task fatCapsule(type: FatCapsule) {
    group 'Build'
//    manifest = project.manifest { from sharedManifest }
    applicationClass project.mainClassName
}

artifacts {
    archives fatCapsule
}

jar {
    manifest = project.manifest { from sharedManifest }
}

// Java/Scala cross-compilation
//sourceSets.main.scala.srcDirs += sourceSets.main.java.srcDirs
//sourceSets.main.java.srcDirs = []

allprojects {
    tasks.withType(ScalaCompile) {
        scalaCompileOptions.useCompileDaemon = true
        scalaCompileOptions.useAnt = false
        scalaCompileOptions.deprecation = false
        scalaCompileOptions.additionalParameters = ['-feature']
        options.incremental = true
        options.deprecation = false
        options.compilerArgs += project.javaLintArgs
    }
}
